; Generated by AutoGUI 2.6.2
#SingleInstance Force
#IfWinActive, ahk_class D3 Main Window Class
#NoEnv
#InstallKeybdHook
SetWorkingDir %A_ScriptDir%
SetBatchLines -1

VERSION:=20210415
TITLE:=Format("暗黑3技能连点器 v{:d}   by Oldsand", VERSION)

ReadCfgFile("d3oldsand.ini", tabs, hotkeys, actions, intervals, others, generals)

Gui -MaximizeBox -MinimizeBox +Owner
tabslen:= ObjCount(StrSplit(tabs, "`|"))
currentProfile:=1
vRunning:=False
profileKeybinding:={}
keysOnHold:=[]
Gui Font, s11
Gui Add, Tab3, x5 y5 w790 h400 gSetTabFocus, %tabs%
Gui Font
Loop, parse, tabs, `|
{
    currentTab := A_Index
    yFirstLine:=85
    y:=yFirstLine
    Gui Tab, %currentTab%
    Loop, 6
    {
        hk:=hotkeys[currentTab][A_Index]
        ac:=actions[currentTab][A_Index]
        iv:=intervals[currentTab][A_Index]
        if A_Index <= 4
        {
            Gui Add, Hotkey, x45 y%y% w75 vskillset%currentTab%s%A_Index%hotkey, %hk%
        }
        Else
        {
            Gui Add, Edit, x45 y%y% w75 vskillset%currentTab%s%A_Index%hotkey +Disabled, %hk%
        }
        Gui Add, DropDownList, x200 y%y% w80 AltSubmit Choose%ac% vskillset%currentTab%s%A_Index%dropdown, 禁用||按住不放||连点||保持Buff
        Gui Add, Edit, vskillset%currentTab%s%A_Index%edit x360 y%y% w61 h21 Number
        Gui Add, Updown, vskillset%currentTab%s%A_Index%updown Range20-30000, %iv%
        y+=50
    }
    Gui Add, Text, x490 y%yFirstLine%, 快速切换：
    yd:=yFirstLine-5
    pfmd:=others[currentTab].profilemethod
    pfhk:=others[currentTab].profilehotkey
    Gui Add, DropDownList, x+5 y%yd% w90 AltSubmit Choose%pfmd% vskillset%currentTab%profilekeybindingdropdown gSetProfileKeybinding, 无||鼠标中键||滚轮向上||滚轮向下||侧键1||侧键2||键盘按键
    Gui Add, Hotkey, x+15 w85 vskillset%currentTab%profilekeybindinghkbox gSetProfileKeybinding +Disabled, %pfhk%
    Gui Add, Text, x490 y+25, 走位辅助：
    pfmv:=others[currentTab].movingmethod
    pfmi:=others[currentTab].movinginterval
    pflm:=others[currentTab].lazymode
    Gui Add, DropDownList, x+5 y120 w70 AltSubmit Choose%pfmv% vskillset%currentTab%movingdropdown gSetMovingHelper, 无||强制站立||强制走位
    Gui Add, Text, vskillset%currentTab%movingtext x+15 y125 +Disabled, 间隔：
    Gui Add, Edit, vskillset%currentTab%movingedit x+5 y120 w61 h21 Number +Disabled
    Gui Add, Updown, vskillset%currentTab%movingupdown Range20-3000 +Disabled, %pfmi%
    Gui Add, Text, x490 y+25, 宏启动方式：
    Gui Add, DropDownList, x+5 y160 w90 AltSubmit Choose%pflm% vskillset%currentTab%profilestartmodedropdown, 懒人模式||仅按下时
    if (currentTab>1)
    {
        Gui Font, s10
        Gui Add, Link, x490 y300, This is a <a href="https://www.autohotkey.com">link</a>
        Gui Font
    }
    Else
    {
        Gui Add, CheckBox, x490 y250 vskillset%currentTab%gambling, 赌博助手
        Gui Font, s10
        Gui Add, Link, x490 y300, This is a <a href="https://www.autohotkey.com">link</a>
        Gui Font
    }
    Gui Add, GroupBox, x20 y50 w125 h340, 技能
    Gui Add, GroupBox, x+30 y50 w130 h340, 策略
    Gui Add, GroupBox, x+30 y50 w110 h340, 执行间隔（毫秒）
    Gui Add, GroupBox, x+30 y50 w300 h150, 额外设置
    Gui Add, GroupBox, y+10 w300 h180, 辅助功能
}
Gui Tab

startRunHK:=generals.starthotkey
startmethod:=generals.startmethod
Gui Font, cRed s10
Gui Add, Text, x470 y8, 宏启动暂停快捷键：
Gui Font
Gui Add, DropDownList, x+5 y5 w90 AltSubmit Choose%startmethod% vStartRunDropdown gSetStartRun, 鼠标右键||鼠标中键||滚轮向上||滚轮向下||侧键1||侧键2||键盘按键
Gui Add, Hotkey, x+5 y5 w70 vStartRunHKinput gSetStartRun, %startRunHK%

Menu, Tray, NoStandard
Menu, Tray, Add, 设置
Menu, Tray, Add, 退出
Menu, Tray, Default, 设置
Menu, Tray, Click, 1
Menu, Tray, Tip, %TITLE%
Menu, Tray, Icon, , , 1

Gosub, SetStartRun
SetTimer, safeGuard, 300
Gui Show, w800 h410, %TITLE%
Return

ReadCfgFile(cfgFileName, ByRef tabs, ByRef hotkeys, ByRef actions, ByRef intervals, ByRef others, ByRef generals){
    global VERSION
    if FileExist(cfgFileName)
    {
        IniRead, ver, %cfgFileName%, General, version
        if (%VERSION% != %ver%)
        {
            MsgBox, 配置文件版本不匹配，如有错误请删除配置文件并手动配置。
        }
        IniRead, gamble, %cfgFileName%, General, gamble
        IniRead, gamble_times, %cfgFileName%, General, gamble_times
        IniRead, startmethod, %cfgFileName%, General, startmethod
        IniRead, starthotkey, %cfgFileName%, General, starthotkey
        generals:={"gamble":gamble, "gamble_times":gamble_times,"startmethod":startmethod, "starthotkey":starthotkey}

        IniRead, tabs, %cfgFileName%
        tabs:=StrReplace(StrReplace(tabs, "`n", "`|"), "General|", "")
        hotkeys:=[]
        actions:=[]
        intervals:=[]
        others:=[]
        Loop, parse, tabs, `|
        {
            cSection:=A_LoopField
            thk:=[]
            tac:=[]
            tiv:=[]
            tos:={}
            Loop, 6
            {
                IniRead, hk, %cfgFileName%, %cSection%, skill_%A_Index%
                IniRead, ac, %cfgFileName%, %cSection%, action_%A_Index%
                IniRead, iv, %cfgFileName%, %cSection%, interval_%A_Index%
                thk.Push(hk)
                tac.Push(ac)
                tiv.Push(iv)
            }
            hotkeys.Push(thk)
            actions.Push(tac)
            intervals.Push(tiv)
            IniRead, pfmd, %cfgFileName%, %cSection%, profilehkmethod
            IniRead, pfhk, %cfgFileName%, %cSection%, profilehkkey
            IniRead, pfmv, %cfgFileName%, %cSection%, movingmethod
            IniRead, pfmi, %cfgFileName%, %cSection%, movinginterval
            IniRead, pflm, %cfgFileName%, %cSection%, lazymode
            tos:={"profilemethod":pfmd, "profilehotkey":pfhk, "movingmethod":pfmv, "movinginterval":pfmi, "lazymode":pflm}
            others.Push(tos)
        }

    }
    Else
    {
        tabs=配置1|配置2
        hotkeys:=[["1","2","3","4","左键","右键"],["1","2","3","4","左键","右键"]]
        actions:=[[1,1,1,1,1,1],[1,1,1,1,1,1]]
        intervals:=[[300,300,300,300,300,300],[300,300,300,300,300,300]]
        others:=[{"profilemethod":1, "profilehotkey":"", "movingmethod":1, "movinginterval":100, "lazymode":1}, {"profilemethod":1, "profilehotkey":"", "movingmethod":1, "movinginterval":100, "lazymode":1}]
        generals:={"gamble":"ph", "gamble_times":15,"startmethod":7, "starthotkey":"F2"}
    }
    Return
}

SaveCfgFile(cfgFileName, tabs, VERSION){
    FileDelete, %cfgFileName%

    IniWrite, %VERSION%, %cfgFileName%, General, version
    IniWrite, "ph", %cfgFileName%, General, gamble
    IniWrite, 15, %cfgFileName%, General, gamble_times
    GuiControlGet, StartRunDropdown
    GuiControlGet, StartRunHKInput
    IniWrite, %StartRunDropdown%, %cfgFileName%, General, startmethod
    IniWrite, %StartRunHKInput%, %cfgFileName%, General, starthotkey

    Loop, parse, tabs, `|
    {
        cSection:=A_Index
        nSction:=A_LoopField
        Loop, 6
        {
            GuiControlGet, skillset%cSection%s%A_Index%hotkey
            GuiControlGet, skillset%cSection%s%A_Index%dropdown
            GuiControlGet, skillset%cSection%s%A_Index%updown
            hk:=skillset%cSection%s%A_Index%hotkey
            ac:=skillset%cSection%s%A_Index%dropdown
            iv:=skillset%cSection%s%A_Index%updown
            IniWrite, %hk%, %cfgFileName%, %nSction%, skill_%A_Index%
            IniWrite, %ac%, %cfgFileName%, %nSction%, action_%A_Index%
            IniWrite, %iv%, %cfgFileName%, %nSction%, interval_%A_Index%
        }
        GuiControlGet, skillset%cSection%profilekeybindingdropdown
        GuiControlGet, skillset%cSection%profilekeybindinghkbox
        pfhkdd:=skillset%cSection%profilekeybindingdropdown
        pfhkbx:=skillset%cSection%profilekeybindinghkbox
        IniWrite, %pfhkdd%, %cfgFileName%, %nSction%, profilehkmethod
        IniWrite, %pfhkbx%, %cfgFileName%, %nSction%, profilehkkey
        GuiControlGet, skillset%cSection%movingdropdown
        GuiControlGet, skillset%cSection%movingupdown
        movingmethod:=skillset%cSection%movingdropdown
        movinginterval:=skillset%cSection%movingupdown
        IniWrite, %movingmethod%, %cfgFileName%, %nSction%, movingmethod
        IniWrite, %movinginterval%, %cfgFileName%, %nSction%, movinginterval
        GuiControlGet, skillset%cSection%profilestartmodedropdown
        lazymode:=skillset%cSection%profilestartmodedropdown
        IniWrite, %lazymode%, %cfgFileName%, %nSction%, lazymode
    }
    Return
}

SetTabFocus:
ControlFocus %A_GuiControl%
Return

SetProfileKeybinding:
    Gui, Submit, NoHide
    mouseKeyArray:=["", "MButton", "WheelUp", "WheelDown", "XButton1", "XButton2", ""]
    global profileKeybinding, tabslen
    Loop, %tabslen%
    {
        currentPage := A_Index
        if (skillset%currentPage%profilekeybindingdropdown = 7)
        {
            GuiControl, Enable, skillset%currentPage%profilekeybindinghkbox
        }
        Else
        { 
            GuiControl, Disable, skillset%currentPage%profilekeybindinghkbox
        }
        Gui, Submit, NoHide

        for key, value in profileKeybinding.Clone()
        {
            if (value = currentPage)
            {
                Hotkey, %key%, SwitchProfile, Off
                Hotkey, +%key%, SwitchProfile, Off
                profileKeybinding.Pop(key)
            }
        }
        voption:=skillset%currentPage%profilekeybindingdropdown
        if voption in 2,3,4,5,6
        {
            ckey:=mouseKeyArray[voption]
            Hotkey, %ckey%, SwitchProfile, on
            Hotkey, +%ckey%, SwitchProfile, on
            profileKeybinding[ckey]:=currentPage
        }
        else if (voption = 7)
        {
            ckey:=skillset%currentPage%profilekeybindinghkbox
            if (ckey)
            {
                Hotkey, %ckey%, SwitchProfile, on
                Hotkey, +%ckey%, SwitchProfile, on
                profileKeybinding[ckey]:=currentPage
            } 
        }
    }
Return

SwitchProfile:
    global profileKeybinding, currentProfile, vRunning
    currentProfile := profileKeybinding[A_ThisHotkey]
    if vRunning
    {
        vRunning := False
        Gosub, StopMarco
        Gosub, MainMacro
    }
Return

SetStartRun:
    Gui, Submit, NoHide
    global startRunHK, tabslen
    startRunMouseKeyArray:=["RButton", "MButton", "WheelUp", "WheelDown", "XButton1", "XButton2", ""]
    if (StartRunDropdown = 7)
    {
        GuiControl, Enable, StartRunHKinput
        newstartRunHK=%StartRunHKinput%
    }
    Else
    {
        if (StartRunDropdown = 1)
        {
            Loop, %tabslen%
            {
                GuiControl, choose, skillset%A_Index%s6dropdown, 1
                GuiControl, Disable, skillset%A_Index%s6dropdown
                GuiControl, Disable, skillset%A_Index%s6updown
                GuiControl, Disable, skillset%A_Index%s6edit
            }
        }
        Else
        {
            Loop, %tabslen%
            {
                GuiControl, Enable, skillset%A_Index%s6dropdown
                GuiControl, Enable, skillset%A_Index%s6updown
                GuiControl, Enable, skillset%A_Index%s6edit
            }
        }
        GuiControl, Disable, StartRunHKinput
        newstartRunHK:=startRunMouseKeyArray[StartRunDropdown]
    }
    Try
    {
        Hotkey, %startRunHK%, MainMacro, off
        Hotkey, +%startRunHK%, MainMacro, off
        Hotkey, %newstartRunHK%, MainMacro, on
        Hotkey, +%newstartRunHK%, MainMacro, on
        startRunHK = %newstartRunHK%
    }
    Gosub, SetProfileKeybinding
    Gosub, SetMovingHelper
Return

SetMovingHelper:
    Gui, Submit, NoHide
    Loop, %tabslen%{
        npage:=A_Index
        if (skillset%npage%movingdropdown = 3)
        {
            GuiControl, Enable, skillset%npage%movingtext
            GuiControl, Enable, skillset%npage%movingedit
            GuiControl, Enable, skillset%npage%movingupdown
        }
        Else
        { 
            GuiControl, Disable, skillset%npage%movingtext
            GuiControl, Disable, skillset%npage%movingedit
            GuiControl, Disable, skillset%npage%movingupdown
        }
    }
Return

MainMacro:
    global vRunning, startRunHK, currentProfile
    GuiControlGet, skillset%currentProfile%profilestartmodedropdown
    lazy:=skillset%currentProfile%profilestartmodedropdown
    switch lazy
    {
        case 1:
             if not vRunning
            {
                Gosub, RunMarco
            }
            Else
            {
                Gosub, StopMarco
            } 
        case 2:
            Gosub, RunMarco
            KeyWait, %startRunHK%
            Gosub, StopMarco
    }
Return

RunMarco:
    global currentProfile, vRunning, keysOnHold
    Gui, Submit, NoHide
    Loop, 6
    {
        GuiControlGet, skillset%currentProfile%s1dropdown
        GuiControlGet, skillset%currentProfile%s1hotkey
        Switch skillset%currentProfile%s%A_Index%dropdown
        {
        Case 2:
            k:=skillset%currentProfile%s%A_Index%hotkey
            send {%k% Down}
            keysOnHold.Push(k)
        Case 3, 4:
            GuiControlGet, skillset%currentProfile%s%A_Index%updown
            interval:=skillset%currentProfile%s%A_Index%updown
            SetTimer, spamSkillKey%A_Index%, %interval%
        Default:
            SetTimer, spamSkillKey%A_Index%, off
        }
    }
    GuiControlGet, skillset%currentProfile%movingdropdown
    Switch skillset%currentProfile%movingdropdown
    {
        case 2:
            send {LShift Down}
            keysOnHold.Push("LShift")
        case 3:
            GuiControlGet, skillset%currentProfile%movingedit
            iv:=skillset%currentProfile%movingedit
            SetTimer, forceMoving, %iv%
    }
    vRunning := True 
Return

StopMarco:
    global keysOnHold, vRunning
    Loop, 6
    {
        SetTimer, spamSkillKey%A_Index%, off
    }
    SetTimer, forceMoving, off
    for i, key in keysOnHold{
        send {%key% up}
    }
    keysOnHold:=[]
    vRunning := False
Return

spamSkillKey1:
    global currentProfile
    GuiControlGet, skillset%currentProfile%s1dropdown
    k:=skillset%currentProfile%s1hotkey
    switch skillset%currentProfile%s1dropdown
    {
        case 3,4:
            send %k%
    }   
Return

spamSkillKey2:
    global currentProfile
    GuiControlGet, skillset%currentProfile%s2hotkey
    GuiControlGet, skillset%currentProfile%s2dropdown
    k:=skillset%currentProfile%s2hotkey
    switch skillset%currentProfile%s2dropdown
    {
        case 3,4:
            send %k%
    }
Return

spamSkillKey3:
    global currentProfile
    GuiControlGet, skillset%currentProfile%s3hotkey
    GuiControlGet, skillset%currentProfile%s3dropdown
    k:=skillset%currentProfile%s3hotkey
    switch skillset%currentProfile%s3dropdown
    {
        case 3,4:
            send %k%
    }
Return

spamSkillKey4:
    global currentProfile
    GuiControlGet, skillset%currentProfile%s4hotkey
    GuiControlGet, skillset%currentProfile%s4dropdown
    k:=skillset%currentProfile%s4hotkey
    switch skillset%currentProfile%s4dropdown
    {
        case 3,4:
            send %k%
    }
Return

spamSkillKey5:
    global currentProfile
    GuiControlGet, skillset%currentProfile%s5dropdown
    switch skillset%currentProfile%s5dropdown
    {
        case 3,4:
            Click
    }
Return

spamSkillKey6:
    global currentProfile
    GuiControlGet, skillset%currentProfile%s6dropdown
    switch skillset%currentProfile%s6dropdown
    {
        case 3,4:
            Click Right
    }
Return

forceMoving:
    send e
Return

safeGuard:
    global vRunning, tabslen
    If !WinActive("ahk_class D3 Main Window Class")
    {
        Gosub, StopMarco
    }
    Else
    {
        if vRunning
        {
            Loop, %tabslen%
            {
                currentTab:=A_Index
                Loop, 4
                {
                    GuiControl, Disable, skillset%currentTab%s%A_Index%hotkey
                }
            }
        }
        Else
        {
            Loop, %tabslen%
            {
                currentTab:=A_Index
                Loop, 4
                {
                    GuiControl, Enable, skillset%currentTab%s%A_Index%hotkey
                }
            }
        }
    }   
Return

GuiEscape:
GuiClose:
    Gui, Submit
    SaveCfgFile("d3oldsand.ini", tabs, VERSION)
Return

设置:
    Gui, Show,, %TIELE%
Return

退出:
    SaveCfgFile("d3oldsand.ini", tabs, VERSION)
ExitApp
